
plugins {
    id 'java'
    id 'io.spring.dependency-management'
    id 'jacoco'
    id 'org.barfuin.gradle.jacocolog'
    id 'maven-publish'
}

group = 'paymark.apiproxyservice'
version = '0.0.1'
sourceCompatibility = '11'

repositories {
    maven {
        url "${artifactoryBaseUrl}java-all/"
        credentials {
            username = "${credentialsArtifactoryIdentity}"
            password = "${credentialsArtifactoryPassword}"
        }
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}


def integrationTest = sourceSets.create('integrationTest')

configurations[integrationTest.implementationConfigurationName].extendsFrom(configurations.testImplementation)
configurations[integrationTest.runtimeOnlyConfigurationName].extendsFrom(configurations.testRuntimeOnly)



tasks.named('test') {
    useJUnitPlatform()
}

def integrationTestTask = tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'
    useJUnitPlatform()

    testClassesDirs = integrationTest.output.classesDirs
    classpath = configurations[integrationTest.runtimeClasspathConfigurationName] + integrationTest.output

    shouldRunAfter(tasks.named('test'))
}

tasks.named('check') {
    dependsOn(integrationTestTask)
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.1'
    integrationTestImplementation project
}

